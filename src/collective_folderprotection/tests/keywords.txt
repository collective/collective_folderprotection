*** Settings ***

Library  Selenium2Library  timeout=5 seconds  implicit_wait=0.5 seconds

*** Variables ***

${PORT} =  55001
${ZOPE_URL} =  http://localhost:${PORT}
${PLONE_URL} =  ${ZOPE_URL}/plone
${BROWSER} =  Firefox

${protected_folder_url} =  ${PLONE_URL}/protected
${internal_protected} =  ${protected_folder_url}/a-page
${not_protected_folder_url} =  ${PLONE_URL}/not-protected
${internal_not_protected} =  ${not_protected_folder_url}/a-page

${MANAGER_USER_NAME} =  manager
${MANAGER_PASSWORD} =  manager
${CONTRIBUTOR_USER_NAME} =  contributor
${CONTRIBUTOR_PASSWORD} =  contributor

*** Keywords ***

Start browser
    Open browser  ${PLONE_URL}  browser=${BROWSER}

Goto Homepage
    Go to   ${PLONE_URL}
    Page should contain  Plone site

Log out
    Go to  ${PLONE_URL}/logout
    Page should contain  logged out

Log in
    [Arguments]  ${userid}  ${password}

    Go to  ${PLONE_URL}/login_form
    Page should contain element  __ac_name
    Input text  __ac_name  ${userid}
    Input text  __ac_password  ${password}
    Click Button  Log in
    Goto Homepage

Log In As Site Owner
    Log out
    Log in  ${SITE_OWNER_NAME}  ${SITE_OWNER_PASSWORD}

Log In As Test User
    Log out
    Log in  ${TEST_USER_NAME}  ${TEST_USER_PASSWORD}

Log In As Manager User
    Log out
    Log in  ${MANAGER_USER_NAME}  ${MANAGER_PASSWORD}

Log In As Contributor User
    Log out
    Log in  ${CONTRIBUTOR_USER_NAME}  ${CONTRIBUTOR_PASSWORD}

Open Menu
    [Arguments]  ${elementId}

    Element Should Not Be Visible  css=dl#${elementId} dd.actionMenuContent
    Click link  css=dl#${elementId} dt.actionMenuHeader a
    Wait until keyword succeeds  1  5  Element Should Be Visible  css=dl#${elementId} dd.actionMenuContent

Open Add New Menu
    Open Menu  plone-contentmenu-factories

Click Add Folderish Protected
    Open Add New Menu
    Click Link  link=Folderish-Protected
    Page Should Contain  Add Folderish-Protected

Click Add Folderish Not Protected
    Open Add New Menu
    Click Link  link=Folderish-Not-Protected
    Page Should Contain  Folderish-Not-Protected

Click Add Page
    Open Add New Menu
    Click Link  link=Page
    Page Should Contain  Page

Create Protected Folder
    [arguments]  ${title} 
    Click Add Folderish Protected
    Input Text  css=input#form-widgets-IDublinCore-title  ${title}
    Click Button  Save
    Page Should Contain  Item created

Create Not Protected Folder
    [arguments]  ${title}

    Click Add Folderish Not Protected
    Input Text  css=input#form-widgets-IDublinCore-title  ${title}
    Click Button  Save
    Page Should Contain  Item created

Create Page
    [arguments]  ${title}  ${summary}

    Click Add Page
    Input Text  css=input#title  ${title}
    Input Text  css=textarea#description  ${summary}
    Click Button  Save
    Page Should Contain  Changes saved.
