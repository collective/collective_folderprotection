*** Settings ***

Library  Selenium2Library  timeout=5 seconds  implicit_wait=0.5 seconds
Resource  keywords.txt
Variables  plone/app/testing/interfaces.py

Suite Setup  Start browser
Suite Teardown  Close All Browsers

*** Variables ***

${BROWSER} =  firefox

*** Test Cases ***

Owner should be allowed to access protected folder
    Goto Homepage
    Log In As Contributor User
    Create Protected Folder  Protected
    Go to   ${protected_folder_url}
    Page Should Not Contain  This resource is password protected

Owner should be able to access the 'Assign password' view
    Goto Homepage
    Log In As Contributor User
    Create Protected Folder  Protected
    Go to   ${protected_folder_url}
    Page Should Contain  Assign password
    Click Link  link=Assign password
    Page Should Contain  Choose a password to protect this object and, if it is a folder, its children.

Owner should not see the 'Assign password' view for a not protected folder
    Goto Homepage
    Log In As Contributor User
    Create Not Protected Folder  Not-Protected
    Go to   ${not_protected_folder_url}
    Page Should Not Contain  Assign password

If there are 2 protected folders, one with password and the other without, then show 2 different icons in folder_contents view
    Goto Homepage
    Log In As Contributor User
    Create Protected Folder  Protected
    Go to   ${PLONE_URL}/folder_contents
    Page Should Not Contain Image  xpath=//img[@src="${PLONE_URL}/++resource++resources/lock_locked_16.png"]
    Page Should Contain Image  xpath=//img[@src="${PLONE_URL}/++resource++resources/lock_unlocked_16.png"]
    Go to   ${protected_folder_url}
    Click Link  link=Assign password
    Input Text  css=input#form-widgets-password  thepassword
    Click Button  Save
    Go to   ${PLONE_URL}/folder_contents
    Page Should Contain Image  xpath=//img[@src="${PLONE_URL}/++resource++resources/lock_locked_16.png"]
    Page Should Not Contain Image  xpath=//img[@src="${PLONE_URL}/++resource++resources/lock_unlocked_16.png"]
    Goto Homepage
    Create Protected Folder  ProtectedNoPassword
    Go to   ${PLONE_URL}/folder_contents
    Page Should Contain Image  xpath=//img[@src="${PLONE_URL}/++resource++resources/lock_locked_16.png"]
    Page Should Contain Image  xpath=//img[@src="${PLONE_URL}/++resource++resources/lock_unlocked_16.png"]

If one assign a blank password, the password protection is removed
    Goto Homepage
    Log In As Contributor User
    Create Protected Folder  Protected
    Go to   ${PLONE_URL}/folder_contents
    Go to   ${protected_folder_url}
    Click Link  link=Assign password
    Input Text  css=input#form-widgets-password  thepassword
    Click Button  Save
    Go to   ${protected_folder_url}
    Click Link  link=Assign password
    Click Button  Save
    Page Should Contain  This content is not going to be password protected.
    